group 'com.coretex.core'
version 'H-1.0'

buildscript {
    repositories {
        mavenCentral()
//        mavenLocal()
        jcenter()
        maven { url "http://repo.coretexplatform.com/artifactory/coretex_repo-libs-release/"}
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://jitpack.io' }
        maven { url "https://repo.spring.io/libs-release-remote" }
        maven { url "https://jetbrains.bintray.com/trove4j/" }
    }
    dependencies {
        classpath(group: 'com.coretex.build.plugin', name: 'build.plugin', version: '1.1-Beta')
        classpath(group: 'com.bmuschko', name: 'gradle-docker-plugin', version: '6.6.1')
    }
}
wrapper {
    gradleVersion = '6.8.2'
}

apply plugin: 'distribution'
apply from: "gradle-build/config.gradle"
apply plugin: 'build.plugin'
apply from: "gradle-build/subprojects.gradle"
apply plugin: 'com.bmuschko.docker-remote-api'

distributions {
    main {
        distributionBaseName = 'coretex-web-platform'
        contents {
            from projectDir
            exclude 'tmp'
            exclude '**/*.iml'
            exclude '**/.idea'
            exclude '**/.gradle'
            exclude '**/node_modules'
            exclude '**/jsbuild'
            exclude '**/webSrc'
            exclude '**/genSrc'
            exclude '**/classes'
            exclude '**/libs'
            exclude 'build'
            exclude 'data'
        }
    }
}

//distTar.enabled = false
//distZip.enabled = false

import com.bmuschko.gradle.docker.tasks.image.*

task createDockerfile(type: Dockerfile, dependsOn: distTar) {
    destFile.set(project.layout.buildDirectory.file('distributions/Dockerfile'))
    from('openjdk:15')
    addFile("coretex-web-platform-H-1.0.tar", "/opt/coretex/")
    workingDir("/opt/coretex/coretex-web-platform-H-1.0")
    environmentVariable('JAVA_OPTS', '-Djava.awt.headless=true -server -Xms48m -Xmx512M -XX:MaxPermSize=512m -XX:+HeapDumpOnOutOfMemoryError -XX:HeapDumpPath=/opt/tmp/heapdump.bin')
    exposePort(8080)
}
//
task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = file(createDockerfile.destFile).parentFile
    images.add('gerasimenkoart/coretex_commerce:latest')
}

